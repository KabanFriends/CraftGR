plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    modApi("com.terraformersmc:modmenu:${rootProject.mod_menu_version}") { transitive(false) }
    modApi "dev.isxander:yet-another-config-lib:${rootProject.yacl_version}-fabric"

    include "com.github.umjammer:jlayer:${rootProject.jlayer_version}"
    include("org.java-websocket:Java-WebSocket:${rootProject.java_websocket_version}") {
        exclude module: "slf4j-api"
    }

    modRuntimeOnly("com.terraformersmc:modmenu:${rootProject.mod_menu_version}") { transitive(false) }
    modRuntimeOnly "dev.isxander:yet-another-config-lib:${rootProject.yacl_version}-fabric"

    runtimeOnly "com.github.umjammer:jlayer:${rootProject.jlayer_version}"
    runtimeOnly "org.java-websocket:Java-WebSocket:${rootProject.java_websocket_version}"
}

processResources {
    inputs.property "version", rootProject.version

    // Dev
    from(project(":common").file("src/main/resources/craftgr.accesswidener")) {
        into("/");
    }

    filesMatching("fabric.mod.json") {
        expand "version": rootProject.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set "mc${rootProject.minecraft_version}-fabric"
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
