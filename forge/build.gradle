plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

loom {
    forge {
        mixinConfig "craftgr-common.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    modApi("me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_config_version}")

    shadowCommon("javazoom:jlayer:${rootProject.jlayer_version}") { transitive = false }

    implementation "javazoom:jlayer:${rootProject.jlayer_version}"
}

processResources {
    inputs.property "version", rootProject.version

    filesMatching("META-INF/mods.toml") {
        expand "version": rootProject.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set "mc${rootProject.minecraft_version}-forge"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
