plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive false }

    include "org.apache.commons:commons-math3:${rootProject.math3_version}"
    include "com.github.umjammer:jlayer:${rootProject.jlayer_version}"
    include("org.java-websocket:Java-WebSocket:${rootProject.java_websocket_version}") {
        exclude module: "slf4j-api"
    }

    modRuntimeOnly("dev.isxander:yet-another-config-lib:${rootProject.yacl_version}-neoforge") {
        exclude module: "kotlinforforge-neoforge"
    }

    runtimeOnly "org.apache.commons:commons-math3:${rootProject.math3_version}"
    runtimeOnly "com.github.umjammer:jlayer:${rootProject.jlayer_version}"
    runtimeOnly "org.java-websocket:Java-WebSocket:${rootProject.java_websocket_version}"
    runtimeOnly "org.quiltmc.parsers:gson:0.2.1"
}

processResources {
    inputs.property "version", rootProject.version

    filesMatching("META-INF/neoforge.mods.toml") {
        expand "version": rootProject.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set "mc${rootProject.minecraft_version}-neoforge"

    atAccessWideners.add("craftgr.accesswidener")
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
